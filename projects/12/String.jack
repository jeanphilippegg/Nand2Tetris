// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {

    field Array array;
    field int length, arrayMaxLength;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        // We cannot create an array of size 0. So we create an array of at least 1.
        if (maxLength = 0) {
            let array = Array.new(1);
        }
        else {
            let array = Array.new(maxLength);
        }

        let length = 0;
        let arrayMaxLength = maxLength;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do array.dispose();
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return array[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let array[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (length < arrayMaxLength) {
            let array[length] = c;
            let length = length + 1;
        }
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (length > 0) {
            let length = length - 1;
        }
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int v, i;

        let v = 0;

        if ((length > 0) & (array[0] = 45)) {
            let i = 1;
        }
        else {
            let i = 0;
        }

        while (i < length) {
            let v = (v * 10) + (array[i] - 48);
            let i = i + 1;
        }

        if ((length > 0) & (array[0] = 45)) {
            let v = -v;
        }

        return v;
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        let length = 0;
        if (val < 0) {
            do appendChar(45);
            let val = -val;
        }
        do _recSetInt(val);
        return;
    }

    method void _recSetInt(int n) {
        var int lastDigit, c;

        let lastDigit = n - ((n / 10) * 10);    // n % 10
        let c = lastDigit + 48;

        if (n < 10) {
            do appendChar(c);
        }
        else {
            do _recSetInt(n / 10);
            do appendChar(c);
        }
        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
